#version 450
#extension GL_ARB_separate_shader_objects : enable

#define PAGE_SIZE 512 // 2^9 TODO push constants?

layout(std430, set = 1, binding = 4) buffer readonly PageTable
{
	uint counter;
	uint nodes[];
} table;

layout(std430, set = 1, binding = 5) buffer PagePool
{
	uint data[];
} pool;

layout(std430, set = 1, binding = 6) buffer writeonly CompactedClusters
{
	uint counter;
	uint data[];
} comp;

shared uint clusterCounter;

layout(local_size_x = 64) in;
void main()
{
	if (gl_LocalInvocationIndex == 0)
		clusterCounter = 0;

	barrier();

	for (uint i = gl_LocalInvocationIndex; i < PAGE_SIZE * table.counter; i += gl_WorkGroupSize.x)
	{
		if (pool.data[i] > 0)
		{
			uint index = atomicAdd(clusterCounter, 1);
			comp.data[index] = pool.data[i];
			pool.data[i] = index;
		}
	}

	barrier();

	if (gl_LocalInvocationIndex == 0)
	{
		comp.counter = clusterCounter;
	}
}