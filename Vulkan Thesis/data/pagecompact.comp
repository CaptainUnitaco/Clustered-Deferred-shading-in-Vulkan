#version 450
#extension GL_ARB_separate_shader_objects : enable

#define PAGE_SIZE 512 // 2^9 TODO push constants?
#define LOCAL_SIZE 1024
layout(std430, set = 1, binding = 4) buffer readonly PageTable
{
	uint counter;
	uint nodes[];
} table;

layout(std430, set = 1, binding = 5) buffer PagePool
{
	uint data[];
} pool;

layout(std430, set = 1, binding = 6) buffer writeonly CompactedClusters
{
	uint counter;
	uint data[];
} comp;

layout(std430, set = 1, binding = 1) buffer writeonly LightsOut
{
	uint count;
	uint lights[];
} lightsOut;

shared uint clusterCounter;

layout(local_size_x = LOCAL_SIZE) in;
void main()
{
	uint tid = gl_LocalInvocationIndex;

	if (tid == 0)
		clusterCounter = 0;

	barrier();

	for (uint i = tid; i < PAGE_SIZE * table.counter; i += LOCAL_SIZE)
	{
		if (pool.data[i] > 0)
		{
			uint index = atomicAdd(clusterCounter, 1);
			comp.data[index] = pool.data[i];
			pool.data[i] = index;
		}
	}

	barrier();

	if (gl_LocalInvocationIndex == 0)
	{
		comp.counter = clusterCounter;
		
		// prepare lights out for next dispatch
		lightsOut.lights[0] = 0; // used as dummy for cluster without light
		lightsOut.count = 1;
	}
}